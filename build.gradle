plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'uk.co.conoregan'
version '1.0.0'
description = 'ShowRenamer'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    modularity.inferModulePath = false
}

tasks.named('compileJava') {
    modularity.inferModulePath = false
}

application {
    mainModule = 'uk.co.conoregan.showrenamer'
    mainClass = 'uk.co.conoregan.showrenamer.ShowRenamerApplication'
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'
    implementation 'commons-io:commons-io:2.12.0'
    implementation 'com.github.holgerbrandl:themoviedbapi:1.15'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.3.1'
}

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
}

test {
    moduleOptions {
        runOnClasspath = true
    }

    // enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Show Renamer'
    }
    jpackage {
        installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--verbose']
    }
}
