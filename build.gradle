plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
}

dependencies {
    // logging
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-simple:2.0.16'

    // testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.1'
    testImplementation 'org.mockito:mockito-core:5.12.0'

    // util
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-lang3:3.15.0'
    implementation 'uk.co.conoregan:themoviedbapi:2.1.2'
}

group 'uk.co.conoregan'
version '2.0.1'
description = 'ShowRenamer'

sourceCompatibility = '21'
targetCompatibility = '21'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    modularity.inferModulePath = false
}

checkstyle {
    toolVersion '10.17.0'
    configFile file("config/checkstyle/checkstyle.xml")
}

tasks.named('compileJava') {
    modularity.inferModulePath = false
}

application {
    mainModule = 'uk.co.conoregan.showrenamer'
    mainClass = 'uk.co.conoregan.showrenamer.ShowRenamerApplication'
}

javafx {
    version = "22"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
}

test {
    moduleOptions {
        runOnClasspath = true
    }

    // enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // always run tests, even when nothing changed.
    dependsOn 'cleanTest'
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'Show Renamer'
    }

    forceMerge('slf4j')

    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        icon = "src/main/resources/images/show-renamer-icon.$imgType"

        installerOptions += [
            '--vendor', 'Conor Egan',
            '--app-version', version
        ]

        if (currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--verbose']
        }
    }
}
